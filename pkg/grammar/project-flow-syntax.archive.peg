#
# NOTE: This PEG is for historical reference only.
# The source of truth (and continuing development) will be with Tree-sitter.
#

Start <- Commands / Response / InteractiveSession

Commands <- Command (Newline Command)*

InteractiveSession <- (Command Newline Response Newline?)+

Command <- Dependency / TaskSplitOperation / EntityCreateOrUpdate / EntityRemove / TaskExplodeImplode / ClusterOperation / Comment / ResourceAssignment

Comment <- CommentSigil [^\n\r]*

Dependency <- RoadmapItems NegationOp? RequiredByOp RoadmapItems
TaskSplitOperation <- (NewTaskSigil RequiredByOp Task) / (Task RequiredByOp NewTaskSigil)
EntityCreateOrUpdate <- Entity
EntityRemove <- NegationOp Entity
TaskExplodeImplode <- Task ExplodeOp Number / Tasks ImplodeOp Task
ClusterOperation <- NegationOp? ClusterSigil Identifier KvSeparator RoadmapItems
ResourceAssignment <- Resources NegationOp? RequiredByOp Tasks

RoadmapItems <- RoadmapItem (Separator RoadmapItem)*
RoadmapItem <- Task / Milestone
Entity <- RoadmapItem / Resource
Tasks <- Task (Separator Task)*
Task <- Identifier Attributes?
Milestone <- MilestoneSigil Identifier Attributes?
Resource <- ResourceSigil Identifier Attributes?
Resources <- Resource (Separator Resource)*

Attributes <- '(' (NegationOp / (Attribute (Separator Attribute)*)) ')'
Attribute <- Identifier KvSeparator (QuotedValue / Value / NegationOp)
QuotedValue <- '"' (([^"\\\n\r]) / ('\\' .) / Newline)* '"'
Value <- [^,)"]+

Response <- Number (Whitespace [^\n\r]*)?

Identifier <- [A-Za-z][A-Za-z0-9_-]*

ClusterSigil <- Whitespace? '@' Whitespace?
CommentSigil <- Whitespace? '#' Whitespace?
MilestoneSigil <- Whitespace? '%' Whitespace?
NewTaskSigil <- Whitespace? '*' Whitespace?
ResourceSigil <- Whitespace? '$' Whitespace?

ExplodeOp <- Whitespace? '!' Whitespace?
ImplodeOp <- NegationOp ExplodeOp
RequiredByOp <- Whitespace? '>' Whitespace?
NegationOp <- Whitespace? '~' Whitespace?

KvSeparator <- Whitespace? ':' Whitespace?
Separator <- Whitespace? ',' Whitespace? / Whitespace
Number <- [0-9]+
Whitespace <- [ \t]+
Newline <- [\n\r]+